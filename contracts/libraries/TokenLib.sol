pragma solidity 0.5.8;

import "../interfaces/IPoly.sol";
import "./StatusCodes.sol";
import "../modules/UpgradableModuleFactory.sol";
import "../interfaces/IDataStore.sol";
import "../tokens/SecurityTokenStorage.sol";
import "../interfaces/ITransferManager.sol";
import "../modules/UpgradableModuleFactory.sol";
import "openzeppelin-solidity/contracts/math/SafeMath.sol";
import "../modules/PermissionManager/IPermissionManager.sol";
import "openzeppelin-solidity/contracts/token/ERC20/IERC20.sol";

library TokenLib {

    using SafeMath for uint256;

    struct EIP712Domain {
        string  name;
        uint256 chainId;
        address verifyingContract;
    }

    struct Acknowledgment {
        string text;
    }

    bytes32 constant EIP712DOMAIN_TYPEHASH = keccak256(
        "EIP712Domain(string name,uint256 chainId,address verifyingContract)"
    );

    bytes32 constant ACK_TYPEHASH = keccak256(
        "Acknowledgment(string text)"
    );

    bytes32 internal constant WHITELIST = "WHITELIST";
    bytes32 internal constant INVESTORSKEY = 0xdf3a8dd24acdd05addfc6aeffef7574d2de3f844535ec91e8e0f3e45dba96731; //keccak256(abi.encodePacked("INVESTORS"))

    // Emit when Module get upgraded from the securityToken
    event ModuleUpgraded(uint8[] _types, address _module);
    // Emit when Module is archived from the SecurityToken
    event ModuleArchived(uint8[] _types, address _module);
    // Emit when Module is unarchived from the SecurityToken
    event ModuleUnarchived(uint8[] _types, address _module);
    // Emit when Module get removed from the securityToken
    event ModuleRemoved(uint8[] _types, address _module);
    // Emit when the budget allocated to a module is changed
    event ModuleBudgetChanged(uint8[] _moduleTypes, address _module, uint256 _oldBudget, uint256 _budget);
    // Emit when document is added/removed
    event DocumentUpdated(bytes32 indexed _name, string _uri, bytes32 _documentHash);
    event DocumentRemoved(bytes32 indexed _name, string _uri, bytes32 _documentHash);

    function hash(EIP712Domain memory _eip712Domain) internal pure returns (bytes32) {
        return keccak256(
            abi.encode(
                EIP712DOMAIN_TYPEHASH,
                keccak256(bytes(_eip712Domain.name)),
                _eip712Domain.chainId,
                _eip712Domain.verifyingContract
            )
        );
    }

    function hash(Acknowledgment memory _ack) internal pure returns (bytes32) {
        return keccak256(abi.encode(ACK_TYPEHASH, keccak256(bytes(_ack.text))));
    }

    function recoverFreezeIssuanceAckSigner(bytes calldata _signature) external view returns (address) {
        Acknowledgment memory ack = Acknowledgment("I acknowledge that freezing Issuance is a permanent and irrevocable change");
        return extractSigner(ack, _signature);
    }

    function recoverDisableControllerAckSigner(bytes calldata _signature) external view returns (address) {
        Acknowledgment memory ack = Acknowledgment("I acknowledge that disabling controller is a permanent and irrevocable change");
        return extractSigner(ack, _signature);
    }

    function extractSigner(Acknowledgment memory _ack, bytes memory _signature) internal view returns (address) {
        bytes32 r;
        bytes32 s;
        uint8 v;

        // Check the signature length
        if (_signature.length != 65) {
            return (address(0));
        }

        // Divide the signature in r, s and v variables
        // ecrecover takes the signature parameters, and the only way to get them
        // currently is to use assembly.
        // solhint-disable-next-line no-inline-assembly
        assembly {
            r := mload(add(_signature, 0x20))
            s := mload(add(_signature, 0x40))
            v := byte(0, mload(add(_signature, 0x60)))
        }

        // Version of signature should be 27 or 28, but 0 and 1 are also possible versions
        if (v < 27) {
            v += 27;
        }

        // If the version is correct return the signer address
        if (v != 27 && v != 28) {
            return (address(0));
        }

        bytes32 DOMAIN_SEPARATOR = hash(
            EIP712Domain(
                {
                    name: "Polymath",
                    chainId: 1,
                    verifyingContract: address(this)
                }
            )
        );

        // Note: we need to use `encodePacked` here instead of `encode`.
        bytes32 digest = keccak256(abi.encodePacked(
            "\x19\x01",
            DOMAIN_SEPARATOR,
            hash(_ack)
        ));
        return ecrecover(digest, v, r, s);
    }

    /**
    * @notice Archives a module attached to the SecurityToken
    * @param _moduleData Storage data
    */
    function archiveModule(SecurityTokenStorage.ModuleData storage _moduleData) external {
        require(!_moduleData.isArchived, "Module archived");
        require(_moduleData.module != address(0), "Module missing");
        /*solium-disable-next-line security/no-block-members*/
        emit ModuleArchived(_moduleData.moduleTypes, _moduleData.module);
        _moduleData.isArchived = true;
    }

    /**
    * @notice Unarchives a module attached to the SecurityToken
    * @param _moduleData Storage data
    */
    function unarchiveModule(IModuleRegistry _moduleRegistry, SecurityTokenStorage.ModuleData storage _moduleData) external {
        require(_moduleData.isArchived, "Module unarchived");
        /*solium-disable-next-line security/no-block-members*/
        // Check the version is still valid - can only be false if token was upgraded between unarchive / archive
        _moduleRegistry.useModule(_moduleData.moduleFactory, true);
        emit ModuleUnarchived(_moduleData.moduleTypes, _moduleData.module);
        _moduleData.isArchived = false;
    }

    /**
    * @notice Upgrades a module attached to the SecurityToken
    * @param _moduleData Storage data
    */
    function upgradeModule(IModuleRegistry _moduleRegistry, SecurityTokenStorage.ModuleData storage _moduleData) external {
        require(_moduleData.module != address(0), "Module missing");
        //Check module is verified and within version bounds
        _moduleRegistry.useModule(_moduleData.moduleFactory, true);
        // Will revert if module isn't upgradable
        UpgradableModuleFactory(_moduleData.moduleFactory).upgrade(_moduleData.module);
        emit ModuleUpgraded(_moduleData.moduleTypes, _moduleData.module);
    }

    /**
    * @notice Removes a module attached to the SecurityToken
    * @param _module address of module to unarchive
    */
    function removeModule(
        address _module,
        mapping(uint8 => address[]) storage _modules,
        mapping(address => SecurityTokenStorage.ModuleData) storage _modulesToData,
        mapping(bytes32 => address[]) storage _names
    )
        external
    {
        require(_modulesToData[_module].isArchived, "Not archived");
        require(_modulesToData[_module].module != address(0), "Module missing");
        /*solium-disable-next-line security/no-block-members*/
        emit ModuleRemoved(_modulesToData[_module].moduleTypes, _module);
        // Remove from module type list
        uint8[] memory moduleTypes = _modulesToData[_module].moduleTypes;
        for (uint256 i = 0; i < moduleTypes.length; i++) {
            _removeModuleWithIndex(moduleTypes[i], _modulesToData[_module].moduleIndexes[i], _modules, _modulesToData);
            /* modulesToData[_module].moduleType[moduleTypes[i]] = false; */
        }
        // Remove from module names list
        uint256 index = _modulesToData[_module].nameIndex;
        bytes32 name = _modulesToData[_module].name;
        uint256 length = _names[name].length;
        _names[name][index] = _names[name][length - 1];
        _names[name].length = length - 1;
        if ((length - 1) != index) {
            _modulesToData[_names[name][index]].nameIndex = index;
        }
        // Remove from modulesToData
        delete _modulesToData[_module];
    }

    /**
    * @notice Internal - Removes a module attached to the SecurityToken by index
    */
    function _removeModuleWithIndex(
        uint8 _type,
        uint256 _index,
        mapping(uint8 => address[]) storage _modules,
        mapping(address => SecurityTokenStorage.ModuleData) storage _modulesToData
    )
        internal
    {
        uint256 length = _modules[_type].length;
        _modules[_type][_index] = _modules[_type][length - 1];
        _modules[_type].length = length - 1;

        if ((length - 1) != _index) {
            //Need to find index of _type in moduleTypes of module we are moving
            uint8[] memory newTypes = _modulesToData[_modules[_type][_index]].moduleTypes;
            for (uint256 i = 0; i < newTypes.length; i++) {
                if (newTypes[i] == _type) {
                    _modulesToData[_modules[_type][_index]].moduleIndexes[i] = _index;
                }
            }
        }
    }

    /**
    * @notice allows owner to increase/decrease POLY approval of one of the modules
    * @param _module module address
    * @param _change change in allowance
    * @param _increase true if budget has to be increased, false if decrease
    */
    function changeModuleBudget(
        address _module,
        uint256 _change,
        bool _increase,
        IERC20 _polyToken,
        mapping(address => SecurityTokenStorage.ModuleData) storage _modulesToData
    )
        external
    {
        require(_modulesToData[_module].module != address(0), "Module missing");
        uint256 currentAllowance = _polyToken.allowance(address(this), _module);
        uint256 newAllowance;
        if (_increase) {
            require(IPoly(address(_polyToken)).increaseApproval(_module, _change), "IncreaseApproval fail");
            newAllowance = currentAllowance.add(_change);
        } else {
            require(IPoly(address(_polyToken)).decreaseApproval(_module, _change), "Insufficient allowance");
            newAllowance = currentAllowance.sub(_change);
        }
        emit ModuleBudgetChanged(_modulesToData[_module].moduleTypes, _module, currentAllowance, newAllowance);
    }

    /**
     * @notice Queries a value at a defined checkpoint
     * @param _checkpoints is array of Checkpoint objects
     * @param _checkpointId is the Checkpoint ID to query
     * @param _currentValue is the Current value of checkpoint
     * @return uint256
     */
    function getValueAt(SecurityTokenStorage.Checkpoint[] storage _checkpoints, uint256 _checkpointId, uint256 _currentValue) external view returns(uint256) {
        //Checkpoint id 0 is when the token is first created - everyone has a zero balance
        if (_checkpointId == 0) {
            return 0;
        }
        if (_checkpoints.length == 0) {
            return _currentValue;
        }
        if (_checkpoints[0].checkpointId >= _checkpointId) {
            return _checkpoints[0].value;
        }
        if (_checkpoints[_checkpoints.length - 1].checkpointId < _checkpointId) {
            return _currentValue;
        }
        if (_checkpoints[_checkpoints.length - 1].checkpointId == _checkpointId) {
            return _checkpoints[_checkpoints.length - 1].value;
        }
        uint256 min = 0;
        uint256 max = _checkpoints.length - 1;
        while (max > min) {
            uint256 mid = (max + min) / 2;
            if (_checkpoints[mid].checkpointId == _checkpointId) {
                max = mid;
                break;
            }
            if (_checkpoints[mid].checkpointId < _checkpointId) {
                min = mid + 1;
            } else {
                max = mid;
            }
        }
        return _checkpoints[max].value;
    }

    /**
     * @notice Stores the changes to the checkpoint objects
     * @param _checkpoints is the affected checkpoint object array
     * @param _newValue is the new value that needs to be stored
     */
    function adjustCheckpoints(SecurityTokenStorage.Checkpoint[] storage _checkpoints, uint256 _newValue, uint256 _currentCheckpointId) external {
        //No checkpoints set yet
        if (_currentCheckpointId == 0) {
            return;
        }
        //No new checkpoints since last update
        if ((_checkpoints.length > 0) && (_checkpoints[_checkpoints.length - 1].checkpointId == _currentCheckpointId)) {
            return;
        }
        //New checkpoint, so record balance
        _checkpoints.push(SecurityTokenStorage.Checkpoint({checkpointId: _currentCheckpointId, value: _newValue}));
    }

    /**
    * @notice Keeps track of the number of non-zero token holders
    * @param _holderCount Number of current token holders
    * @param _from Sender of transfer
    * @param _to Receiver of transfer
    * @param _value Value of transfer
    * @param _balanceTo Balance of the _to address
    * @param _balanceFrom Balance of the _from address
    * @param _dataStore address of data store
    */
    function adjustInvestorCount(
        uint256 _holderCount,
        address _from,
        address _to,
        uint256 _value,
        uint256 _balanceTo,
        uint256 _balanceFrom,
        IDataStore _dataStore
    )
        external
        returns(uint256)
    {
        uint256 holderCount = _holderCount;
        if ((_value == 0) || (_from == _to)) {
            return holderCount;
        }
        // Check whether receiver is a new token holder
        if ((_balanceTo == 0) && (_to != address(0))) {
            holderCount = holderCount.add(1);
            if (!_isExistingInvestor(_to, _dataStore)) {
                _dataStore.insertAddress(INVESTORSKEY, _to);
                //KYC data can not be present if added is false and hence we can set packed KYC as uint256(1) to set added as true
                _dataStore.setUint256(_getKey(WHITELIST, _to), uint256(1));
            }
        }
        // Check whether sender is moving all of their tokens
        if (_value == _balanceFrom) {
            holderCount = holderCount.sub(1);
        }

        return holderCount;
    }

    /**
     * @notice Used to attach a new document to the contract, or update the URI or hash of an existing attached document
     * @param name Name of the document. It should be unique always
     * @param uri Off-chain uri of the document from where it is accessible to investors/advisors to read.
     * @param documentHash hash (of the contents) of the document.
     */
    function setDocument(
        mapping(bytes32 => SecurityTokenStorage.Document) storage document,
        bytes32[] storage docNames,
        mapping(bytes32 => uint256) storage docIndexes,
        bytes32 name,
        string calldata uri,
        bytes32 documentHash
    )
        external
    {
        require(name != bytes32(0), "Bad name");
        require(bytes(uri).length > 0, "Bad uri");
        if (document[name].lastModified == uint256(0)) {
            docNames.push(name);
            docIndexes[name] = docNames.length;
        }
        document[name] = SecurityTokenStorage.Document(documentHash, now, uri);
        emit DocumentUpdated(name, uri, documentHash);
    }

    /**
     * @notice Used to remove an existing document from the contract by giving the name of the document.
     * @dev Can only be executed by the owner of the contract.
     * @param name Name of the document. It should be unique always
     */
    function removeDocument(
        mapping(bytes32 => SecurityTokenStorage.Document) storage document,
        bytes32[] storage docNames,
        mapping(bytes32 => uint256) storage docIndexes,
        bytes32 name
    )
        external
    {
        require(document[name].lastModified != uint256(0), "Not existed");
        uint256 index = docIndexes[name] - 1;
        if (index != docNames.length - 1) {
            docNames[index] = docNames[docNames.length - 1];
            docIndexes[docNames[index]] = index + 1;
        }
        docNames.length--;
        emit DocumentRemoved(name, document[name].uri, document[name].docHash);
        delete document[name];
    }

    /**
     * @notice Validate transfer with TransferManager module if it exists
     * @dev TransferManager module has a key of 2
     * @param modules Array of addresses for transfer managers
     * @param modulesToData Mapping of the modules details
     * @param from sender of transfer
     * @param to receiver of transfer
     * @param value value of transfer
     * @param data data to indicate validation
     * @param transfersFrozen whether the transfer are frozen or not.
     * @return bool
     */
    function verifyTransfer(
        address[] storage modules,
        mapping(address => SecurityTokenStorage.ModuleData) storage modulesToData,
        address from,
        address to,
        uint256 value,
        bytes memory data,
        bool transfersFrozen
    )
        public //Marked public to avoid stack too deep error
        view
        returns(bool, bytes32)
    {
        if (!transfersFrozen) {
            bool isInvalid = false;
            bool isValid = false;
            bool isForceValid = false;
            // Use the local variables to avoid the stack too deep error
            bytes32 appCode;
            for (uint256 i = 0; i < modules.length; i++) {
                if (!modulesToData[modules[i]].isArchived) {
                    (ITransferManager.Result valid, bytes32 reason) = ITransferManager(modules[i]).verifyTransfer(from, to, value, data);
                    if (valid == ITransferManager.Result.INVALID) {
                        isInvalid = true;
                        appCode = reason;
                    } else if (valid == ITransferManager.Result.VALID) {
                        isValid = true;
                    } else if (valid == ITransferManager.Result.FORCE_VALID) {
                        isForceValid = true;
                    }
                }
            }
            // Use the local variables to avoid the stack too deep error
            isValid = isForceValid ? true : (isInvalid ? false : isValid);
            return (isValid, isValid ? bytes32(StatusCodes.code(StatusCodes.Status.TransferSuccess)): appCode);
        }
        return (false, bytes32(StatusCodes.code(StatusCodes.Status.TransfersHalted)));
    }

    function canTransfer(
        bool success,
        bytes32 appCode,
        address to,
        uint256 value,
        uint256 balanceOfFrom
    )
        external
        pure
        returns (byte, bytes32)
    {
        if (!success)
            return (StatusCodes.code(StatusCodes.Status.TransferFailure), appCode);

        if (balanceOfFrom < value)
            return (StatusCodes.code(StatusCodes.Status.InsufficientBalance), bytes32(0));

        if (to == address(0))
            return (StatusCodes.code(StatusCodes.Status.InvalidReceiver), bytes32(0));

        // Balance overflow can never happen due to totalsupply being a uint256 as well
        // else if (!KindMath.checkAdd(balanceOf(_to), _value))
        //     return (0x50, bytes32(0));

        return (StatusCodes.code(StatusCodes.Status.TransferSuccess), bytes32(0));
    }

    function _getKey(bytes32 _key1, address _key2) internal pure returns(bytes32) {
        return bytes32(keccak256(abi.encodePacked(_key1, _key2)));
    }

    function _isExistingInvestor(address _investor, IDataStore dataStore) internal view returns(bool) {
        uint256 data = dataStore.getUint256(_getKey(WHITELIST, _investor));
        //extracts `added` from packed `whitelistData`
        return uint8(data) == 0 ? false : true;
    }

}
